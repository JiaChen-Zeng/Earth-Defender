def mod(a, b)
	out -> a - (a / b * b)

def max(a, b)
	out -> cond(a < b, b, a)

def min(a, b)
	out -> cond(a < b, a, b)

def abs(number)
	out -> cond(number > 0, number, -number)

def pow(base, power)
	assertEx(-1 < power, ERROR_ILLEGAL_ARGUMENT)
	number -> 1
	while power
		number -> number * base
		power -> power - 1
	out -> number

# Babylonian method
def sqrt(number)
	assertEx(-1 < number, ERROR_ILLEGAL_ARGUMENT)
	x -> number
	nextX -> max(number / 2, 1)
	while x != nextX
		x -> nextX
		nextX -> (x + (number / x)) / 2
	out -> x

# Credit: Stephen J. Chapman. "Example 6.4 – Random Number Generator". "MATLAB Programming for Engineers". 2015. pp. 253–256.
const RANDOM_MULTIPLIER -> 8121
const RANDOM_INCREMENT -> 28411
const RANDOM_MODULUS -> 134456
def random(number)
	memory[_randomSeed] -> mod(RANDOM_MULTIPLIER * memory[_randomSeed] + RANDOM_INCREMENT, RANDOM_MODULUS)
	out -> mod(memory[_randomSeed], number)